use 'libs/stack.baabnq';


jump Math::Skip;


lab Math::Inc;
    asm '
    jmS Stack::Swap;    
    pla;
    set 1;
    add;
    pha;
    jmS Stack::Swap;    
    ';
    return;

lab Math::Dec;
    asm '
    jmS Stack::Swap;    
    pla;
    set 1;
    sub;
    pha;
    jmS Stack::Swap;    
    ';
    return;


lab Math::Min;
    pull _ReturnAddr; "nasty, but best solution in this case
    pull _1;
    pull _2;
    push _ReturnAddr; "save
    push _2;
    push _1;
    
    sub Stack::Swap ~ _1 < _2;
      
    pull __; "pull smaller value
    sub Stack::Swap;
    return;

lab Math::Max;
    pull _ReturnAddr; "nasty, but best solution in this case
    pull _1;
    pull _2;
    push _ReturnAddr; "save
    push _2;
    push _1;

    sub Stack::Swap ~ _1 > _2;
       
    pull __; "pull smaller value
    sub Stack::Swap;
    return;

"(Value::Int, Offset::Int) => (Value::Int)
"Value::Int =<< Offset::Int
lab Math::Shg;
    sub Stack::Swap;
    pull _2;
    sub Stack::Swap;
    pull _1;

    push _1 << _2;
    sub Stack::Swap;
    return;    

"(Value::Int, Offset::Int) => (Value::Int)
"Value::Int =>> Offset::Int
lab Math::Shs;
    sub Stack::Swap;
    pull _2;
    sub Stack::Swap;
    pull _1;

    push _1 >> _2;
    sub Stack::Swap;
    return;    
    


lab Math::Add;
    asm '
    jmS Stack::Swap;    
    pla;
    sAD -Temp;
    lDR -Temp;

    jmS Stack::Swap;    
    pla;
    add;
    pha;
    
    jmS Stack::Swap;
    ';
    return;

lab Math::Sub;
    asm '
    jmS Stack::Swap;    
    pla;
    sAD -Temp;
    lDR -Temp;

    jmS Stack::Swap;    
    pla;
    sub;
    pha;
    
    jmS Stack::Swap;
    ';
    return;




lab Math::MulSafe;
    put _arg1 = 0;
    put _arg2 = 0;
    put _output = 0;

    sub Stack::Swap;
    pull _arg1;
    sub Stack::Swap;
    pull _arg2;

    jump Math::MulSafe::Exit ~ _arg1 == 0;
    jump Math::MulSafe::Exit ~ _arg2 == 0;
    
    lab Math::MulSafe::Loop; 
       
        jump Math::MulSafe::SkipAdd ~ _arg1 & 1 == 0;
            put _output = _output + _arg2;
        lab Math::MulSafe::SkipAdd;

        put _arg2 = _arg2 << 1;
        put _arg1 = _arg1 >> 1;

        jump Math::MulSafe::Loop ~ _arg1 != 0;
    lab Math::MulSafe::Exit;

    push _output;
    sub Stack::Swap;
return;



lab Math::Mul;
    asm '
    clr;
    sAD -output;

    jmS Stack::Swap;
    pla;
    sAD -arg1;
    jmS Stack::Swap;    
    pla;
    sAD -arg2;
    
    jm0 Math::Mul::Done;
    lDA -arg1;
    jm0 Math::Mul::Done;
                
    lab Math::Mul::Loop;

    lDA -arg1;
    set 1;
    and;

    jm0 Math::Mul::Skip;
    lDA -output;
    lDR -arg2;
    add;
    sAD -output;
    lab Math::Mul::Skip;
    
    lDA -arg2;
    shg;
    sAD -arg2;
    lDA -arg1;
    shs;
    sAD -arg1;
    
    jm0 Math::Mul::Done;
    got Math::Mul::Loop;
    lab Math::Mul::Done;
             
    lDA -output;    
    pha;
    jmS Stack::Swap;    
    ';
    return;


"lab Math::Div;
"i know, this is not a very efficient division alogrithem, but i don't care
"call this subroutine like this;
"push dividend
"push divisor
"sub Math:div
"pull output
"pull remainder

"put _Math::divisor = 0;
"put _Math::dividend = 0;
"put _Math::output = 0;

"pull _Math::returnAddr;
"pull _Math::divisor;
"pull _Math::dividend;

"jump Math::divExit ~ _Math::divisor > _Math::dividend;
"lab Math::divLoop;

"put _Math::output = _Math::output + 1;
"put _Math::dividend = _Math::dividend - _Math::divisor;

"jump Math::divLoop ~ _Math::dividend > _Math::divisor;
"jump Math::divLoop ~ _Math::dividend == _Math::divisor;
"lab Math::divExit;

"push _Math::dividend;
"push _Math::output;
"push _Math::returnAddr;
"return;

"translated from https://en.wikipedia.org/wiki/Division_algorithm#Long_division
"dividend
"divisor
lab Math::Div; 
    sub Stack::Swap;
    pull _d;
    sub Stack::Swap;    
    pull _n;


    put _q = 0;
    put _r = 0;
    put _i = 15;
    
    lab Math::Div::Loop;
        put _r = (_r << 1) | ((_n >> _i) & 1);
        jump Math::Div::SkipSub ~ _d > _r;
            put _r = _r - _d;
            put _q = _q | (1 << _i);
        lab Math::Div::SkipSub;
        put _i = _i - 1;
    jump Math::Div::Loop ~ _i + 1 > 0;

    push _q;
    sub Stack::Swap;
    return;

lab Math::Mod; 
    sub Stack::Swap;    
    pull _d;
    sub Stack::Swap;    
    pull _n;


    put _q = 0;
    put _r = 0;
    put _i = 15;
    
    lab Math::Mod::Loop;
        put _r = (_r << 1) | ((_n >> _i) & 1);
        jump Math::Mod::SkipSub ~ _d > _r;
            put _r = _r - _d;
            put _q = _q | (1 << _i);
        lab Math::Mod::SkipSub;
        put _i = _i - 1;
    jump Math::Mod::Loop ~ _i + 1 > 0;

    push _r;
    sub Stack::Swap;
    return;


lab Math::Skip;
